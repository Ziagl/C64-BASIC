10 REM ***************
20 REM ***         ***
30 REM *** PACKMAN ***
40 REM ***         ***
50 REM ***************
60 REM
70 GOSUB 1000
80 GOSUB 2000
90 GOSUB 3000
100 END
1000 REM
1010 REM *** VORBEREITUNGEN ***
1020 REM
1030 DIM CO(3,2), SP(3)
1040 DIM FA$(15)
1050 FOR I=0 TO 15
1060 :READ FA$(I)
1070 NEXT I
1080 DATA "{black}","{white}","{red}","{cyan}","{purple}","{green}","{blue}","{yellow}"
1090 DATA "{orange}","{brown}","{pink}","{dark gray}","{gray}","{light green}","{light blue}","{light gray}"
1100 PT=0
1110 PM=3:NM=4
1120 POKE 650,128
1130 POKE 53280,0:POKE 53281,0
1140 PRINT "{clear}{reverse on}{white}{space*39}"
1150 FOR I=1 TO 19 STEP 3
1160 :POKE 211,0:POKE 214,I:SYS 58640
1170 :PRINT "{reverse on}{white}{space}{reverse off}{space*37}{reverse on}{space}"
1180 :PRINT "{reverse on}{white}{space}{right}{light blue}{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}";
1190 :PRINT "{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}{white}{space}"
1200 :PRINT "{reverse on}{white}{space}{right}{light blue}{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}";
1210 :PRINT "{space*3}{right}{space*3}{right}{space*3}{right}{space*3}{right}{white}{space}"
1220 NEXT I
1230 PRINT "{reverse on}{white}{space}{reverse off}{space*37}{reverse on}{space}"
1250 PRINT "{green}{space}Z Z Z";
1260 DIM PM(8,2)
1270 FOR I=5 TO 33 STEP 4
1280 :FOR J=4 TO 19 STEP 3
1290 : F$=MID$("{pink}{light green}{yellow}{purple}{green}",RND(1)*5+1,1)
1300 : POKE 211,I:POKE 214,J:SYS 58640
1310 : PRINT "{reverse off}";F$;"*"
1320 :NEXT J
1330 NEXT I
1340 DEF FN BS(V)=PEEK(1024+X2+40*Y2+V)
1350 DEF FN FA(V)=PEEK(55296+X2+40*Y2+V) AND 15
1360 RETURN
2000 REM
2010 REM *** SPIEL ***
2020 REM
2030 CO(1,1)=1:CO(1,2)=1
2040 CO(2,1)=37:CO(2,2)=1
2050 CO(3,1)=37:CO(3,2)=22
2060 DE(1)=1:DE(2)=4:DE(3)=3
2070 FOR I=1 TO 3
2080 :POKE 211,CO(I,1):POKE 214,CO(I,2):SYS 58640
2090 :PRINT "{white}W";
2100 :SP$(I)=" "
2110 NEXT I
2120 FOR I=1 TO NM
2130 :PM(I,1)=I
2140 :PM(I,2)=22
2150 NEXT I
2160 MX=NM:MY=22
2170 FOR I=1 TO NM
2180 :POKE 211,PM(I,1):POKE 214,PM(I,2):SYS 58640
2190 :PRINT "{green}Z";
2200 NEXT I
2210 PR=1:KL=0:MM=0
2220 PM=PM-1:IF PM<0 THEN RETURN
2230 POKE 211,15:POKE 214,24:SYS 58640
2240 PRINT "{white}<<< fertig >>>";
2250 FOR I=1 TO 2000:NEXT I
2260 PRINT "{left*15}{space*15}";
2270 GOSUB 2480
2280 IF PT=48 THEN RETURN
2290 GOSUB 2700
2300 IF KL=0 THEN GOTO 2270
2310 FOR I=1 TO 3
2320 :POKE 211,CO(I,1):POKE 214,CO(I,2):SYS 58640
2330 :PRINT SP$(I);
2340 NEXT I
2350 FOR I=1 TO NM
2360 :POKE 211,PM(I,1):POKE 214,PM(I,2):SYS 58640
2370 :PRINT " ";
2380 NEXT I
2390 POKE 211,2*PM+1:POKE 214,23:SYS 58640
2400 PRINT "{space*2}";
2410 GOTO 2030
2480 GT=((MX+3)/4)=INT((MX+3)/4) AND ((MY+2)/3)=INT((MY+2)/3)
2490 IF NOT GT THEN GOTO 2510
2500 GET K$:IF K$="" THEN GOTO 2510
2501 IF K$="d" THEN PR=1
2502 IF K$="w" THEN PR=2
2503 IF K$="a" THEN PR=3
2504 IF K$="s" THEN PR=4
2510 X2=MX:Y2=MY
2520 IF PR=1 THEN X2=X2+1
2530 IF PR=2 THEN Y2=Y2-1
2540 IF PR=3 THEN X2=X2-1
2550 IF PR=4 THEN Y2=Y2+1
2560 IF X2<1 OR X2>37 OR Y2<1 OR Y2>22 THEN RETURN
2570 IF FN BS(0)<>42 THEN GOTO 2600
2580 PT=PT+1:IF PT=48 THEN RETURN
2590 T=4+INT(PT/12):IF T<>NM THEN NM=T:PM(NM,1)=X2:PM(NM,2)=MY
2600 POKE 211,PM(1,1):POKE 214,PM(1,2):SYS 58640
2610 PRINT " ";
2620 POKE 211,X2:POKE 214,Y2:SYS 58640:PRINT "{green}Z";
2630 FOR I=1 TO NM-1
2640 :PM(I,1)=PM(I+1,1)
2650 :PM(I,2)=PM(I+1,2)
2660 NEXT I
2670 PM(NM,1)=X2:PM(NM,2)=Y2
2680 MX=X2:MY=Y2
2690 RETURN
2700 FOR K=1 TO 2
2710 :MM=MM+1:IF MM>3 THEN MM=1
2720 :I=MM
2730 :TX=CO(I,1):TY=CO(I,2)
2740 :GT=((TX+3)/4)=INT((TX+3)/4) AND ((TY+2)/3)=INT((TY+2)/3) 
2750 :IF NOT GT THEN GOTO 2776
2760 :ON RND(1)*8+1 GOTO 2770,2770,2770,2770,2770,2775,2775,2776
2770 :IF RND(1)>0.5  THEN GOTO 2773
2771 :IF MX<TX THEN DE(I)=3:GOTO 2776
2772 :IF MX>TX THEN DE(I)=1:GOTO 2776
2773 :IF MY<TY THEN DE(I)=2:GOTO 2776
2774 :IF MY>TY THEN DE(I)=4:GOTO 2776
2775 :DE(I)=INT(RND(1)*4)+1
2776 :X2=TX:Y2=TY:TD=DE(I)
2780 :IF TD=1 THEN X2=X2+1
2790 :IF TD=2 THEN Y2=Y2-1
2800 :IF TD=3 THEN X2=X2-1
2810 :IF TD=4 THEN Y2=Y2+1
2830 :IF X2<1 OR X2>37 OR Y2<1 OR Y2>22 THEN GOTO 2930
2840 :POKE 211,TX:POKE 214,TY:SYS 58640
2850 :PRINT SP$(I);
2860 :SP$(I)=FA$(FN FA(0)):T=FN BS(O):IF T=87 THEN T=119
2870 :IF T=90 THEN T=122
2890 :SP$(I)=SP$(I)+CHR$(T)
2900 :POKE 211,X2:POKE 214,Y2:SYS 58640
2910 :PRINT "{white}W";
2920 :CO(I,1)=X2:CO(I,2)=Y2
2930 :FOR I=1 TO 3
2940 : TX=CO(I,1)
2950 : TY=CO(I,2)
2960 : FOR J=1 TO NM
2970 :  IF TX=PM(J,1) AND TY=PM(J,2) THEN KL=1:GOTO 2995
2980 : NEXT J
2990 :NEXT I
2995 NEXT K:RETURN
3000 REM
3010 REM *** ENDE ***
3020 REM 
3030 PRINT "{clear}"
3040 PRINT "das spiel ist beendet !!!"
3050 IF PT=48 THEN PRINT "sie haben alle punkte !":RETURN
3060 PRINT "sie haben";PT;"punkte..."
3070 RETURN