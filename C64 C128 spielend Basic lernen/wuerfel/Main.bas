10 REM ***************
20 REM ***         ***
30 REM *** WUERFEL ***
40 REM ***         ***
50 REM ***************
60 REM
70 GOSUB 1000
80 GOSUB 2000
90 GOSUB 3000
100 END
1000 REM
1010 REM *** VORBEREITUNGEN ***
1020 REM
1030 PRINT "{clear}{reverse on}{space*40}{reverse off}";
1040 FOR I=1 TO 20
1050 :PRINT "{reverse on}{space}{reverse off}{space*38}{reverse on}{space}{reverse off}";
1060 NEXT
1070 PRINT "{reverse on}{space*40}{reverse off}";
1080 GE=1500
1090 RETURN
1200 REM
1210 REM *** UNTERPROGRAMM 1 ***
1220 REM 
1230 Z1=INT(RND(1)*6)+1
1240 Z2=INT(RND(1)*6)+1
1250 GOSUB 1300
1260 GET A$:IF A$="" THEN GOTO 1260
1270 RETURN
1300 REM
1310 REM *** UNTERPROGRAMM 2 ***
1320 REM
1330 POKE 211,11:POKE 214,10:SYS 58640
1340 ON Z1 GOSUB 1410,1450,1490,1530,1570,1610
1350 POKE 211,25:POKE 214,10:SYS 58640
1360 ON Z2 GOSUB 1410,1450,1490,1530,1570,1610
1370 RETURN
1410 PRINT "{space*3}{down}{left*3}";
1420 PRINT " Q{space}{down}{left*3}";
1430 PRINT "{space*3}"
1440 RETURN
1450 PRINT "Q{space*2}{down}{left*3}";
1460 PRINT "{space*3}{down}{left*3}";
1470 PRINT "{space*2}Q"
1480 RETURN
1490 PRINT "Q{space*2}{down}{left*3}";
1500 PRINT " Q{space}{down}{left*3}";
1510 PRINT "{space*2}Q"
1520 RETURN
1530 PRINT "Q Q{down}{left*3}";
1540 PRINT "{space*3}{down}{left*3}";
1550 PRINT "Q Q"
1560 RETURN
1570 PRINT "Q Q{down}{left*3}";
1580 PRINT " Q{space}{down}{left*3}";
1590 PRINT "Q Q"
1600 RETURN
1610 PRINT "Q Q{down}{left*3}";
1620 PRINT "Q Q{down}{left*3}";
1630 PRINT "Q Q"
1640 RETURN
1800 REM
1810 REM *** UNTERPROGRAMM 3 ***
1820 REM
1830 POKE 211,0:POKE 214,Y:SYS 58640
1840 PRINT "{space*40}";
1850 PRINT "{space*40}";
1860 RETURN
2000 REM
2010 REM *** EIGTL. SPIEL ***
2020 REM
2030 Y=22:GOSUB 1800
2040 Y=4:GOSUB 1800
2050 Y=6:GOSUB 1800
2060 PRINT "{home}{down*3}{right*3}sie haben{space*6}{left*5}";GE;"{left}{space}dm.{space*4}"
2070 POKE 214,23:SYS 58640
2080 INPUT "wieviel moechten sie riskieren ";AN$
2090 RI=VAL(AN$)
2100 IF RI<0 OR RI>GE THEN GOTO 2030
2110 IF RI=0 THEN RETURN
2120 POKE 211,3:POKE 214,4:SYS 58640:PRINT "wette:";RI;"{left}{space}"
2130 Y=22:GOSUB 1800
2140 POKE 211,9:POKE 214,22:SYS 58640:PRINT "(die wette laeuft...)"
2150 PRINT "{up}{space}druecken sie eine taste um zu stoppen.";
2160 GOSUB 1200:SU=Z1+Z2
2170 Y=22:GOSUB 1800
2180 POKE 211,3:POKE 214,5:SYS 58640:PRINT "gewuerfelt:";SU;"{left}{space}"
2190 IF SU>3 AND SU<12 THEN GOTO 2230
2200 PRINT "{up}{right*3}rausgeschmissen ..."
2210 RI=-RI
2220 FOR PA=1 TO 1500:NEXT PA
2230 GOTO 2520
2240 IF SU<>7 AND SU<>11 THEN GOTO 2280
2250 POKE 211,3:POKE 214,7:SYS 58640:PRINT "sie haben diesen wurf gewonnen !"
2260 FOR PA=1 TO 1500:NEXT PA
2270 GOTO 2520
2280 P=SU
2290 FOR PA=1 TO 1500:NEXT PA
2300 POKE 211,3:POKE 214,4:SYS 58640:PRINT "wette:";RI;" punkte:";P
2310 Y=5:GOSUB 1800
2320 POKE 211,2:POKE 214,23:SYS 58640:PRINT "(wette laeuft ... taste druecken)"
2330 GOSUB 1200
2340 SU=Z1+Z2
2350 POKE 211,3:POKE 214,6:SYS 58640:PRINT "{up}gewuerfelt ";SU
2360 IF SU<>7 THEN GOTO 2400
2370 PRINT "{up}{right*3}rausgeschmissen ..."
2380 FOR PA=1 TO 1500:NEXT PA
2390 GOTO 2490
2400 IF SU<>P THEN GOTO 2440
2410 PRINT "{up}{right*3}sie haben ihre punkte !!!"
2420 FOR PA=1 TO 1500:NEXT PA
2430 GOTO 2520
2440 PRINT "{up}{right*3}sie muessen noch einmal wuerfeln ..."
2450 FOR PA=1 TO 1500:NEXT PA
2460 Y=22:GOSUB 1800
2470 Y=5:GOSUB 1800
2480 GOTO 2320
2490 GE=GE-RI
2500 IF GE<=0 THEN RETURN
2510 GOTO 2030
2520 GE=GE+RI
2530 GOTO 2030
3000 REM
3010 REM *** ENDE ***
3020 REM
3030 PRINT "{clear}"
3040 POKE 214,10:SYS 58640
3050 PRINT "sie hoerten mit";GE;"dm auf."
3060 IF GE<100 THEN PRINT "uebung, uebung !":RETURN
3070 IF GE<500 THEN PRINT "immerhin ...":RETURN
3080 IF GE<1000 THEN PRINT "nicht schlecht !":RETURN
3090 PRINT "junge, junge !":RETURN